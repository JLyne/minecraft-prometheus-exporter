plugins {
    id 'java-library'
    id 'maven-publish'
}

rootProject.ext.mavenUserName=""
rootProject.ext.mavenPassword=""

if (file("./build.gradle.local").exists()) {
    apply from: "./build.gradle.local"
}

allprojects {
    group = 'uk.co.notnull'
    version = '2.2.6-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url = uri('https://papermc.io/repo/repository/maven-public/')
        }

        maven {
            url = uri('https://repo.not-null.co.uk/releases/')
        }
    }

    dependencies {
        implementation 'org.eclipse.jetty:jetty-server:9.4.53.v20231009'
        implementation 'io.prometheus:simpleclient_common:0.16.0'
        implementation 'io.prometheus:simpleclient_hotspot:0.16.0'

        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    url = 'https://github.com/JLyne/minecraft-prometheus-exporter'
                    developers {
                        developer {
                            id = 'jim'
                            name = 'James Lyne'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/JLyne/minecraft-prometheus-exporter.git'
                        developerConnection = 'scm:git:ssh://github.com/JLyne/minecraft-prometheus-exporter.git'
                        url = 'https://github.com/JLyne/minecraft-prometheus-exporter'
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username = rootProject.ext.mavenUserName
                    password = rootProject.ext.mavenPassword
                }
                authentication {
                    basic(BasicAuthentication)
                }

                def releasesRepoUrl = "https://repo.not-null.co.uk/releases/" // gradle -Prelease publish
                def snapshotsRepoUrl = "https://repo.not-null.co.uk/snapshots/"
                url = project.hasProperty('release') ? releasesRepoUrl : snapshotsRepoUrl
            }
        }
    }
}

java.sourceCompatibility = JavaVersion.VERSION_21
java.targetCompatibility = JavaVersion.VERSION_21
